task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

subprojects { proj ->

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    description = 'Structurizr'
    group = 'com.func-structurizr'
    version = '0.8.2'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }
        test {
            java {
                srcDir 'test/unit'
            }
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        manifest {
            attributes(
                    "Implementation-Title": "Structurizr for Java",
                    "Implementation-Version": version
            )
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }


    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find {
                            dep -> dep.name == it.artifactId.text()
                        }
                    }.each() {
                        it.scope*.value = 'compile'
                    }
                }
                from components.java
            }
        }
    }

    publishing {
        repositories {
            mavenLocal()
        }
    }

}